&ANALYZE-SUSPEND _VERSION-NUMBER UIB_v9r12
&ANALYZE-RESUME
&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _DEFINITIONS Procedure 
/*------------------------------------------------------------------------
    FILE
    rt_customFunc.p

    DESCRIPTION
    A bunch of useful functions for Roundtable customizations.

    fnRtbGetRootPath -
      Returns the currently selected workspace root path for an object if an
      object is checked in or has a task share-status of "central".  If it is
      checked out and has a share-status that is not central, it returns the 
      root task path.
      
      You must be in a currently running RTB session to use this function as 
      it uses global shared variables.
                      
    fnRtbGetSourcName -
      Returns a comma separated list of paths and files names for
      an object.  Pass it the return-value from fnGetRootPath to get 
      the complete path and filename to an object.
      
    fnRtbGet4glFile -
      Roundtable supports multipart objects.  Prolint really should look at all
      4GL parts, but not for now :) This function returns the first part that 
      I would think Prolint would want to play with.  
      
      It tries to find a .w or .p first.  In the case of a mapped web object, we 
      would want the .w first.  In the case of a form with a .p and multiple 
      includes, we want the .p.
      For example, 
        "g:/workspace/module1/prog.html,,,,,,,,," 
        would return
        "g:/workspace/module1/prog.w".
      
    fnRtbGetLintFile -
      Return the full path filename to the first 4GL part of an object 
      including either the workspace root or task root given only the 
      object recid.  
      
    fnRtbGetRCodeDir -
      Returns the r-code directory for an object.
    
    fnRtbStripPath -
      Pull just the the filename from a full path.  
    
    fnRtbMoveHtmlW -
      Move the .w generated by a RTB HTML compile from the sesssion:temp-dir
      to a location next to the source .html.
    
    AUTHOR
    Gerry Winning
    gwinning@email.com
    jed_knight@yahoo.com 


  ----------------------------------------------------------------------*/
/*          This .W file was created with the Progress AppBuilder.      */
/*----------------------------------------------------------------------*/

/* ***************************  Definitions  ************************** */
   DEFINE VARIABLE gcRtbCurrentWorkspace     AS CHARACTER  NO-UNDO.
   DEFINE VARIABLE giRTBCurrentTaskNum       AS INTEGER    NO-UNDO.
   DEFINE VARIABLE gcRtbCurrentTaskPath      AS CHARACTER  NO-UNDO.
   DEFINE VARIABLE gcRtbCurrentWorkspacePath AS CHARACTER  NO-UNDO.
   DEFINE VARIABLE gcRtbCurrentWorkspaceRoot AS CHARACTER  NO-UNDO.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


&ANALYZE-SUSPEND _UIB-PREPROCESSOR-BLOCK 

/* ********************  Preprocessor Definitions  ******************** */

&Scoped-define PROCEDURE-TYPE Procedure
&Scoped-define DB-AWARE no



/* _UIB-PREPROCESSOR-BLOCK-END */
&ANALYZE-RESUME


/* ************************  Function Prototypes ********************** */

&IF DEFINED(EXCLUDE-fnRtbAllowFileEdit) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbAllowFileEdit Procedure 
FUNCTION fnRtbAllowFileEdit RETURNS LOGICAL
  ( INPUT pcFileName AS CHARACTER )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGet4glFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbGet4glFile Procedure 
FUNCTION fnRtbGet4glFile RETURNS CHARACTER
  ( INPUT pcPartPaths AS CHARACTER )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbFindProlintIniFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbFindProlintIniFile Procedure
FUNCTION fnRtbFindProlintIniFile RETURNS CHARACTER
  (  )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetLintFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbGetLintFile Procedure 
FUNCTION fnRtbGetLintFile RETURNS CHARACTER
  ( INPUT prObject AS ROWID)  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetRCodeDir) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbGetRCodeDir Procedure 
FUNCTION fnRtbGetRCodeDir RETURNS CHARACTER
( INPUT Pobj-type AS CHARACTER,
  INPUT Pobject AS CHARACTER,
  INPUT Proot-dir AS CHARACTER )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetRootPath) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbGetRootPath Procedure 
FUNCTION fnRtbGetRootPath RETURNS CHARACTER
  ( INPUT pcObject    AS CHARACTER,
    INPUT pcWspaceId  AS CHARACTER )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetSourceName) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbGetSourceName Procedure 
FUNCTION fnRtbGetSourceName RETURNS CHARACTER
  ( INPUT Pobj-rowid AS ROWID, 
    INPUT Proot-dir AS CHARACTER )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbMoveHtmlW) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbMoveHtmlW Procedure 
FUNCTION fnRtbMoveHtmlW RETURNS CHARACTER
  ( INPUT prObject AS ROWID )  FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbStripPath) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION-FORWARD fnRtbStripPath Procedure 
FUNCTION fnRtbStripPath RETURNS CHARACTER
  (INPUT pcFullPath AS CHARACTER) FORWARD.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF


/* *********************** Procedure Settings ************************ */

&ANALYZE-SUSPEND _PROCEDURE-SETTINGS
/* Settings for THIS-PROCEDURE
   Type: Procedure
   Allow: 
   Frames: 0
   Add Fields to: Neither
   Other Settings: CODE-ONLY COMPILE
 */
&ANALYZE-RESUME _END-PROCEDURE-SETTINGS

/* *************************  Create Window  ************************** */

&ANALYZE-SUSPEND _CREATE-WINDOW
/* DESIGN Window definition (used by the UIB) 
  CREATE WINDOW Procedure ASSIGN
         HEIGHT             = 15
         WIDTH              = 60.
/* END WINDOW DEFINITION */
                                                                        */
&ANALYZE-RESUME

 


&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _MAIN-BLOCK Procedure 


/* ***************************  Main Block  *************************** */

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


/* ************************  Function Implementations ***************** */

&IF DEFINED(EXCLUDE-fnRtbAllowFileEdit) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbAllowFileEdit Procedure 
FUNCTION fnRtbAllowFileEdit RETURNS LOGICAL
  ( INPUT pcFileName AS CHARACTER ) :
/*------------------------------------------------------------------------------
  Purpose:  Returns TRUE if the given file is allow to be edited.
            Only run if RTB is running, this function will check if the 
            program in pcFileName is checked out on the current task
    Notes:  
------------------------------------------------------------------------------*/
  DEFINE VARIABLE cTemp    AS CHARACTER NO-UNDO.
  DEFINE VARIABLE lCanEdit AS LOGICAL   NO-UNDO INITIAL FALSE.

  DEFINE BUFFER Brtb_ver    FOR rtb.rtb_ver.
  DEFINE BUFFER Brtb_object FOR rtb.rtb_object.

  cTemp = REPLACE( FILE-INFO:FULL-PATHNAME , "~\":U , "/" ).
  IF INDEX( cTemp , "/":U ) <> 0 THEN
    cTemp = SUBSTRING( cTemp , R-INDEX( cTemp , "/" ) + 1 ).  /* Get only the file name */
  
  PUBLISH "evRtbGetCurrentTask":U (OUTPUT giRTBCurrentTaskNum).
        
  /* Locate the object on the current task */
  FIND Brtb_ver WHERE Brtb_ver.task-num   = giRTBCurrentTaskNum 
                 AND Brtb_ver.obj-status = "W":U 
                 AND Brtb_ver.OBJECT     = cTemp NO-LOCK NO-ERROR.
  /* If the object exists, still doesnt mean you cant edit it.
     Could be file is not in RTB */
  IF NOT AVAILABLE Brtb_ver THEN DO:
    FIND Brtb_object WHERE Brtb_object.wspace-id = gcRtbCurrentWorkspace 
                      AND Brtb_object.OBJECT    = cTemp NO-LOCK NO-ERROR.
    lCanEdit = NOT AVAILABLE Brtb_object.  /* Not in RTB ? allow edit */
  END.
  ELSE lCanEdit = TRUE.
  
  RETURN (lCanEdit ).   /* Function return value. */

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGet4glFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbGet4glFile Procedure 
FUNCTION fnRtbGet4glFile RETURNS CHARACTER
  ( INPUT pcPartPaths AS CHARACTER ) :
/*------------------------------------------------------------------------------
  Roundtable supports multipart objects.  Prolint really should look at all
  4GL parts, but not for now :) This function returns the first part that 
  I would think Prolint would want to play with.  
  
  It tries to find a .w or .p first.  In the case of a mapped web object, we 
  would want the .w first.  In the case of a form with a .p and multiple 
  includes, we want the .p.
  For example, 
    "g:/workspace/module1/prog.html,,,,,,,,," 
    would return
    "g:/workspace/module1/prog.w".
------------------------------------------------------------------------------*/

  DEFINE VARIABLE vcPart        AS CHARACTER  NO-UNDO.
  DEFINE VARIABLE viDotPosition AS INTEGER    NO-UNDO.
  DEFINE VARIABLE viCount       AS INTEGER    NO-UNDO.
  DEFINE VARIABLE vcBestPart    AS CHARACTER  NO-UNDO INIT "".
 
  part-loop:
  DO viCount = 1 TO NUM-ENTRIES(pcPartPaths).
    
    ASSIGN vcPart = ENTRY(viCount,pcPartPaths).
    ASSIGN viDotPosition = R-INDEX(vcPart,".").
    
    IF vcPart = "" THEN
      NEXT part-loop.

    CASE SUBSTRING(vcPart,viDotPosition + 1):
      WHEN "w" OR WHEN "p" THEN
        RETURN vcPart.
      WHEN "htm" OR WHEN "html" THEN
      DO:
        ASSIGN vcBestPart = SUBSTRING(vcPart,1,viDotPosition - 1) + ".w".
      END.
    END CASE.
  END. /* part-loop */

  /* if we are still here and vcBestPart is still empty, then
   * just return the first part */
  IF vcBestPart = "" THEN
    ASSIGN vcBestPart = ENTRY(1, pcPartPaths).

  RETURN vcBestPart.   /* Function return value. */

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetLintFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbGetLintFile Procedure 
FUNCTION fnRtbGetLintFile RETURNS CHARACTER
  ( INPUT prObject AS ROWID) :
/*------------------------------------------------------------------------------
  PURPOSE:
  Return the full path filename to the first 4GL part of an object 
  including either the workspace root or task root.
------------------------------------------------------------------------------*/

  DEFINE BUFFER Brtb_object FOR rtb_object.
    
  FIND Brtb_object
    WHERE ROWID(Brtb_object) = prObject
    NO-LOCK
    NO-ERROR.

  IF AVAILABLE Brtb_object THEN
    RETURN fnRtbGet4glFile (INPUT fnRtbGetSourceName(ROWID(Brtb_object),
                            INPUT fnRtbGetRootPath( INPUT Brtb_object.object,
                                              INPUT Brtb_object.wspace-id))).
/*    RETURN fnRtbGet4glFile (INPUT fnRtbGetSourceName(ROWID(Brtb_object)),    */
/*                            INPUT fnRtbGetRootPath( INPUT Brtb_object.object,*/
/*                                              INPUT Brtb_object.wspace-id)). */
  
  ELSE
    RETURN "".

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbFindProlintIniFile) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbFindProlintIniFile Procedure
FUNCTION fnRtbFindProlintIniFile RETURNS CHARACTER
  ( ) :
/*------------------------------------------------------------------------------
  PURPOSE:
  Return the full path filename to file "prolint-rtb-check-in.ini":U
------------------------------------------------------------------------------*/
    DEFINE VARIABLE inifile AS CHARACTER NO-UNDO.

    /* find a workspace specific ini-file */
    inifile = SEARCH(SUBSTITUTE("prolint/custom/rtb-check-in/ws-&1.ini":U, gcRtbCurrentWorkspace)).
    /* not found, then find a default ini-file */
    IF inifile=? THEN
       inifile = SEARCH ("prolint/custom/rtb-check-in/default.ini":U).

    /* for backwards compatibility: search "prolint-rtb-check-in.ini" anywhere in the Propath,
       but only if custom/rtb-check-in/default.ini does not exist
       (so custom/rtb-check-in/default.ini indicates we want the new search structure) */
    IF inifile=? THEN
       inifile = SEARCH("prolint-rtb-check-in.ini":U).
    RETURN inifile.

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetRCodeDir) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbGetRCodeDir Procedure 
FUNCTION fnRtbGetRCodeDir RETURNS CHARACTER
( INPUT Pobj-type AS CHARACTER,
  INPUT Pobject AS CHARACTER,
  INPUT Proot-dir AS CHARACTER ) :

/*--------------------------------------------
Purpose: Determine r-code directory for specified
object.
-------------------------------------------*/

DEFINE VARIABLE Merror AS CHARACTER NO-UNDO.
DEFINE VARIABLE Mpath AS CHARACTER NO-UNDO.

RUN rtb/p/rtb_rnam.p
  (INPUT Pobj-type,
  INPUT Pobject,
  INPUT Proot-dir,
  OUTPUT Mpath). 

Mpath = REPLACE(Mpath,"~\","/").

/* --- Strip filename from path --- */
Mpath = SUBSTRING(Mpath,1,R-INDEX(Mpath,"/")).

/* --- Make sure r-code path exists --- */
RUN rtb/p/rtb_mkpt.p (INPUT Mpath, OUTPUT Merror).

IF Merror <> "" THEN
DO:
  Mpath = "".
  MESSAGE Merror
  VIEW-AS ALERT-BOX ERROR TITLE PROGRAM-NAME(1).
END. 

RETURN Mpath. /* Function return value. */
 
END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetRootPath) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbGetRootPath Procedure 
FUNCTION fnRtbGetRootPath RETURNS CHARACTER
  ( INPUT pcObject    AS CHARACTER,
    INPUT pcWspaceId  AS CHARACTER ) :
/*------------------------------------------------------------------------------
  PURPOSE:
    Returns the currently selected workspace root path for an object if an
    object is checked in or has a task share-status of "central".  If it is
    checked out and has a share-status that is not central, it returns the 
    root task path.
    
    You must be in a currently running RTB session to use this function as 
    it uses global shared variables.
------------------------------------------------------------------------------*/
  DEFINE VARIABLE cTaskPath  AS CHARACTER   NO-UNDO.
  DEFINE BUFFER Brtb_object FOR rtb_object.

  /* Get curent workdpace details */
  PUBLISH "evRtbGetCurrentWorkspace":U (OUTPUT gcRtbCurrentWorkspace).
  ASSIGN
     gcRtbCurrentWorkspacePath = DYNAMIC-FUNCTION('fnRtbGetWorkspacePath':U, gcRtbCurrentWorkspace)    
     gcRtbCurrentWorkspaceRoot = DYNAMIC-FUNCTION('fnRtbGetWorkspaceRoot':U,gcRtbCurrentWorkspace).

  /* Get current task details */
  PUBLISH "evRtbGetCurrentTask":U (OUTPUT giRTBCurrentTaskNum).
  ASSIGN cTaskPath  = DYNAMIC-FUNCTION('fnRtbGetTaskpath':U,giRTBCurrentTaskNum).
  

  FIND Brtb_object
    WHERE Brtb_object.OBJECT    = pcObject
    AND   Brtb_object.wspace-id = pcWspaceId
    NO-LOCK
    NO-ERROR.

  IF NOT AVAILABLE Brtb_object THEN
    RETURN "fail".

  IF Brtb_object.share-status = "central" OR
    ENTRY(1,cTaskPath) = "" 
  THEN
    RETURN ENTRY(1, gcRtbCurrentWorkspacePath).
  ELSE
    RETURN ENTRY(1, cTaskPath).

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbGetSourceName) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbGetSourceName Procedure 
FUNCTION fnRtbGetSourceName RETURNS CHARACTER
  ( INPUT Pobj-rowid AS ROWID, 
    INPUT Proot-dir AS CHARACTER ) :
/*---------------------------------------------------------------------------
  PURPOSE:
    Returns a comma separated list of paths and files names for
    an object.  Pass it the return-value from fnRtbGetRootPath to get
    the complete path and filename to an object.  
---------------------------------------------------------------------------- */
  DEFINE VARIABLE Merror    AS CHARACTER NO-UNDO.
  DEFINE VARIABLE Mpathname AS CHARACTER NO-UNDO.

  ASSIGN Mpathname = DYNAMIC-FUNCTION('fnRtbGetObjectFilenames':U,
                                      INPUT Pobj-rowid,
                                      INPUT Proot-dir).

/*  /*  Get comma delimited list of object parts */   */
/*  RUN rtb/p/rtb_nams.p                              */
/*     (INPUT Pobj-rowid,                             */
/*      INPUT Proot-dir,                              */
/*      OUTPUT Mpathname,                             */
/*      OUTPUT Merror).                               */
/*                                                    */
/*  IF Merror <> "" THEN                              */
/*    MESSAGE Merror                                  */
/*      VIEW-AS ALERT-BOX ERROR TITLE PROGRAM-NAME(1).*/
/*  ELSE                                              */
    Mpathname = REPLACE(Mpathname,"~\","/").

  RETURN Mpathname.   /* Function return value. */

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbMoveHtmlW) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbMoveHtmlW Procedure 
FUNCTION fnRtbMoveHtmlW RETURNS CHARACTER
  ( INPUT prObject AS ROWID ) :
/*------------------------------------------------------------------------------
  PURPOSE:
  Move the .w generated by a RTB HTML compile from the sesssion:temp-dir
  to a location next to the source .html.
------------------------------------------------------------------------------*/

  DEFINE VARIABLE viError      AS INTEGER    NO-UNDO.
  DEFINE VARIABLE vcObjectFile AS CHARACTER  NO-UNDO.
  DEFINE VARIABLE vcLintObject AS CHARACTER  NO-UNDO.

  /* get the filename of the object */
  vcLintObject = 
    fnRtbGetLintFile(prObject).

  IF vcLintObject = "" THEN
    RETURN "Error: Target File Could not be determined".

  /* get the target file with full workspace path */
  vcObjectFile = 
    fnRtbStripPath(INPUT vcLintObject).

  /* rtb converts the .html into a .w in the session:temp-dir */
  ASSIGN vcObjectFile = SESSION:TEMP-DIR + vcObjectFile.  
  OS-COPY VALUE(vcObjectFile) VALUE(vcLintObject).

  viError = OS-ERROR.

  IF viError <> 0 THEN
    RETURN "Error: OS-COPY failed".

  RETURN vcLintObject.

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

&IF DEFINED(EXCLUDE-fnRtbStripPath) = 0 &THEN

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _FUNCTION fnRtbStripPath Procedure 
FUNCTION fnRtbStripPath RETURNS CHARACTER
  (INPUT pcFullPath AS CHARACTER):
/*------------------------------------------------------------------------------
  Notes:  This function removes the path from a given file name.
------------------------------------------------------------------------------*/
  DEFINE VARIABLE viRightSlash AS INTEGER NO-UNDO.

  ASSIGN viRightSlash = R-INDEX(pcFullPath,"/").
  RETURN SUBSTRING( pcFullPath,
                    ( IF viRightSlash > 0 
                        THEN viRightSlash + 1
                        ELSE 1
                     )
                   ).

END FUNCTION.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME

&ENDIF

